SakuraMenuGreetings : nonoverlap
{
	"Hello."
	"How can I help you?"
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	// if SHIORI3FW.Eventid == "OnMouseDoubleClick"; "\b[-1]"
	// else; "\C"
	"\C\![lock,balloonrepaint]\c\0\b[2]%(Refresh)\![set,autoscroll,disable]\![no-pause]"
	
	
	"\n\n\_q"
	
	"\![*]\q[Talk to me,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Say that again,OnLastTalk]"
	"\n\n"
	
	if GalleryOpen; "\![*]\q[Close the gallery,OnToggleGallery,0]"
	else; "\![*]\q[Open the gallery,OnToggleGallery,1]"
	"\n\n"
	
	if SHIORI3FW.DebugMode == 1; "\![*]\q[Debug,OnDebug]\n"
	
	"\![*]\q[Customize,OnCustomize]\n"
	
	//"\![*]\q[Change talk rate,OnTalkRateMenu]\n\n"
	"\![*]\q[Settings,OnSettingsMenu]\n\n"
	
	switch GalleryOpen
	{
		"\![*]\q[Goodbye!,OnGoodbye]"
		"\f[color,disable]\![*]Goodbye!\f[color,default]"
	}
	"\n"
	
	
	"\![*]\q[Nevermind,blank]\![unlock,balloonrepaint]"
	"\_l[0,0]\_q"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
}

OnLastTalk
{
	LastTalk
}

OnToggleGallery
{
	GalleryOpen = reference0
	if !GalleryOpen
	{
		LilGuys = IARRAY
		LilGuys ,= "painting"
	}
	
	Refresh
}

OnCustomize : all
{
	"\C\![lock,balloonrepaint]\c\0\b[2]\![set,autoscroll,disable]\![no-pause]\_q"
	"Painting balloons:\n"
	_bloons = ("Silver,0","Black,1","Copper,3","Navy,2","none","Starry,4")
	
	_i = 0
	foreach _bloons; _bloon
	{
		if _bloon == "none"; {"\n \n[-100]\f[align,right]"; continue}
		if _bloon[1] == SakuraBalloon; "\![*]"
		"\__q[OnChangeSakuraBalloon,%(_bloon[1])]%(_bloon[0])\__q"
		if _i % 2 == 0; "\n \n[-100]\f[align,right]"
		else; "\n\f[align,left]"
		_i++
	}
	
	"\n\n\f[align,left]"
	
	"Guy balloons:\n"
	if GuyBalloon == 1; "\![*]"
	"\q[Dark,OnChangeGuyBalloon,1]\n \n[-100]\f[align,right]"
	if GuyBalloon == 0; "\![*]"
	"\q[Light,OnChangeGuyBalloon,0]"
	"\n\n\f[align,left]"
	
	"Guys:\n"
	if GuyColor == 0; "\![*]"
	"\q[Dark,OnChangeGuyColor,0]\n \n[-100]\f[align,right]"
	if GuyColor == 5; "\![*]"
	"\q[Light,OnChangeGuyColor,5]"
	"\n\n\f[align,left]"
	
	"Painting type:\n"
	_types = ("Landscape","Portrait","Offset","Cluster")
	_i = 1
	foreach _types; _type
	{
		if _i == PaintingType; "\![*]"
		"\__q[OnChangePaintingType,%(_i)]%(_type)\__q"
		if _i % 2 == 1; "\n \n[-100]\f[align,right]"
		else; "\n\f[align,left]"
		_i++
	}
	"\n\n\f[align,left]"
	
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Close,blank]"
	"\![unlock,balloonrepaint]"
	
	""
	/*
	Silver
	Black
	Navy
	Copper
	Starry
	
	Light
	Dark
	
	*/
}

OnChangeSakuraBalloon
{
	SakuraBalloon = reference0
	OnCustomize
}

OnChangeGuyBalloon
{
	GuyBalloon = reference0
	OnCustomize
}

OnChangeGuyColor
{
	GuyColor = reference0
	Refresh
	--
	OnCustomize
}

OnChangePaintingType
{
	PaintingType = reference0
	Refresh
	--
	OnCustomize
}

OnSettingsMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\b[2]%(Refresh)\![set,autoscroll,disable]\![no-pause]\![close,inputbox,OnChangeGuyCap]\_q"
	"\f[align,center]— Settings —\n\f[align,left]"
	
	"\![*]\q[Change talk rate,OnTalkRateMenu]\n"
	"\![*]\q[Change guy scaling,OnGuyScaleMenu]\n"
	"\![*]\q[Change guy limit,OnGuyCapMenu]\n\n"
	"\![*]\q[Replay first boot test,OnFirstBoot.Start]\n\n"
	
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]\![unlock,balloonrepaint]"
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\b[2]%(Refresh)\![set,autoscroll,disable]\![no-pause]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSettingsMenu]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
}

OnGuyScaleMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\b[2]%(Refresh)\![set,autoscroll,disable]\![no-pause]\_q"
	"Current scaling: %(GuyScale)\n\n"
	
	"\![*]\q[200%,OnChangeGuyScale,200]\n"
	"\![*]\q[150%,OnChangeGuyScale,150]\n"
	"\![*]\q[100%,OnChangeGuyScale,100]\n"
	"\![*]\q[50%,OnChangeGuyScale,50]\n"
	"\![*]\q[20%,OnChangeGuyScale,20]\n"
	"\n"
	"\![*]\q[Back,OnSettingsMenu]  \![*]\q[Nevermind,blank]\![unlock,balloonrepaint]"
}

OnChangeGuyScale
{
	GuyScale = TOINT(reference0)
	Refresh
	--
	OnGuyScaleMenu
}

OnGuyCapMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\b[2]%(Refresh)\![set,autoscroll,disable]\![no-pause]\_q"
	"Current cap: %(GuyCap)\n\n"
	"\![open,sliderinput,OnChangeGuyCap,-1,10,5,100]"
	"\n"
	"\![*]\q[Back,OnSettingsMenu]  \![*]\q[Nevermind,OnCancelGuyCapChange]\![unlock,balloonrepaint]"
	
	// "\![*]\q[10 guys,OnGuyCapChange,30]\n"
	// "\![*]\q[1 minute,OnGuyCapChange,60]\n"
	// "\![*]\q[3 minutes,OnGuyCapChange,180]\n"
	// "\![*]\q[5 minutes,OnGuyCapChange,300]\n"
	// "\![*]\q[10 minutes,OnGuyCapChange,600]\n"
}

OnChangeGuyCap
{
	GuyCap = TOINT(reference0)
}

OnCancelGuyCapChange
{
	"\![close,inputbox,OnChangeGuyCap]"
}

OnGoodbye
{
	
}



OnAddLilGuy : all
{
	/*
		Pretentious
		Sweet
		Friendly
		Unimpressed
		Shy
	*/
	_guysona = RAND(5)
	_shape = RAND(5)
	
	if RAND(20) == 0
	{
		_guysona = RAND(3) + 100
		if _guysona == 100; _shape = 5
		elseif _guysona == 101; _shape = 6
		elseif _guysona == 102; _shape = 7
	}
	
	_num = 0
	_filled = 0
	for _i = 0; _i < ARRAYSIZE(LilGuys); _i++
	{
		if LilGuys[_i] == "blank"
		{
			LilGuys[_i] = "%(_guysona)|%(_shape)"
			_filled = 1
			_num = _i
			break
		}
	}
	
	if !_filled; LilGuys ,= "%(_guysona)|%(_shape)"
	
	if _num == 0; _num = ARRAYSIZE(LilGuys) - 1
	_newguy = LilGuys[_num]
	
	
	LOGGING("====================================================================")
	LOGGING("_newguy: %(_newguy)")
	WalkingCharacter = (_num,_newguy[1,'|'])
	_left_edge = TOINT(WalkBorder[0][0])
	
	"\p[%(_num)]\![set,alpha,0]\![set,scaling,%(GuyScale)]\s[1%(_newguy[1,'|'])%(GuyColor)1]"
	
	"\![set,alignmenttodesktop,free]" //+ "free\n\_w[1000]"
	"\![move,--X=%(_left_edge),--Y=%(WalkBorder[0][2]),--base=primaryscreen]" //+ "move to left edge\n\_w[1000]"
	"\![set,alignmenttodesktop,bottom]\_w[100]"// + "bind\n\_w[1000]"
	"\![get,property,OnAddLilGuy2,currentghost.scope(%(WalkingCharacter[0])).rect]"
	
}

OnAddLilGuy2 : all
{
	_top_edge = reference0[1]
	_num = WalkingCharacter[0]
	_newguy = WalkingCharacter[1]
	ERASEVAR("WalkingCharacter")
	_left_edge = TOINT(WalkBorder[0][0])
	_right_edge = TOINT(WalkBorder[0][1])
	_rand_spot = RAND(DisplayWidth - GuyWidth)
	
	_time = ((GuyWidth + _rand_spot) * 2) * GuySpeed
	
	
	"\p[%(_num)]"
	"\![set,alignmenttodesktop,free]" //+ "free\n\_w[1000]"
	"\![move,--X=%(_left_edge - GuyWidth),--Y=%(_top_edge),--base=primaryscreen]\![set,alpha,100]" //+ "move to lefty spot\n\_w[1000]"
	"%(FadeIn)\![move,--X=%(_left_edge + _rand_spot),--Y=%(_top_edge),--time=%(_time),--base=primaryscreen]\s[1%(_newguy)%(GuyColor)0]" //+ "scoot\n\_w[1000]"
	"\![set,alignmenttodesktop,bottom]" //+ "bind\n\_w[1000]"
}

OnRemoveLilGuy
{
	_num = reference0
	if _num == ""; _num = _argv[0]
	
	WalkingCharacter = _num
	"\![get,property,OnRemoveLilGuy2,currentghost.scope(%(WalkingCharacter)).rect]"
}

OnRemoveLilGuy2 : all
{
	_char = TOINT(WalkingCharacter)
	ERASEVAR("WalkingCharacter") //yucky
	
	LOGGING("====================================================================")
	LOGGING("_char: %(_char)")
	LOGGING("LilGuys: %(LilGuys)")
	
	_oldguy = LilGuys[_char]
	LilGuys[_char] = "blank"
	LOGGING("_oldguy: %(_oldguy)")
	_char_left = TOINT(reference0[0])
	_right_edge_painting = TOINT(WalkBorder[0][1])
	_right_edge_char = TOINT(WalkBorder[_char][1])
	_right_edge = 0
	
	LOGGING("====================================================================")
	if _char_left > _right_edge //Special case for if character is on a screen rightwards of painting
	{
		LOGGING("IF")
		_right_edge = _right_edge_char
	}
	else //normal, exit to right of painting's screen. even if you gotta walk for ages for that. whatev can't help you bro
	{
		LOGGING("ELSE")
		_right_edge = _right_edge_painting
	}
	LOGGING("_right_edge: %(_right_edge)")
	
	_time = ABS(ABS(_char_left) - ABS(_right_edge)) * GuySpeed
	LOGGING("_char_left: %(_char_left)")
	//_right_edge
	LOGGING("_oldguy: %(_oldguy)")
	"\p[%(_char)]\s[1%(_oldguy[1,'|'])%(GuyColor)1]"
	"\![move,--X=%(_right_edge),--time=%(_time),--base=primaryscreen]%(FadeOut)\s[-1]"
	
}

//not used yet
OkGuysPresent
{
	_guys = IARRAY
	foreach LilGuys; _lilguy
	{
		if _lilguy == "blank" || _lilguy == "painting"; continue
		elseif TOINT(_lilguy[0,'|']) >= 100; _guys ,= _lilguy[0,'|']
	}
	_guys
}

SodaGuyPresent
{
	ASEARCH("100",OkGuysPresent) != -1
}

JacketGuyPresent
{
	ASEARCH("101",OkGuysPresent) != -1
}

CurlyGuyPresent
{
	ASEARCH("102",OkGuysPresent) != -1
}












OnDebug : all
{
	"\0\b[2]\![no-pause]\_q"
	"\![*]\q[Test movement,OnWalkStart,1]\n"
	"\![*]\q[Add lil guy,OnAddLilGuy]\n"
	"\![*]\q[Remove lil guy,OnRemoveGuyMenu]\n"
	"\![*]\q[Clear all guys,OnClearGuys]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Close,blank]"
}

OnRemoveGuyMenu : all
{
	"\![no-pause]\0\b[2]\![set,autoscroll,disable]\_q"
	"Remove which guy?\n\n"
	
	
	for _i = 1; _i < ARRAYSIZE(LilGuys); _i++
	{
		if LilGuys[_i] == "blank"; continue
		"\![*]\__q[OnRemoveLilGuy,%(_i)]"
		
		_sona = LilGuys[_i][0,'|']
		_shape = LilGuys[_i][1,'|']
		
		switch _sona
		{
			"Pretentious"
			"Sweet"
			"Friendly"
			"Unimpressed"
			"Shy"
		}
		" - "
		switch _shape
		{
			"Small"
			"Tiny"
			"Tall"
			"Large"
			"Wide"
			"Soda"
			"Jacket"
			"Curly"
		}
		
		"\__q\n"
	}
	"\n"
	"\![*]\q[Back,OnDebug]  \![*]\q[Close,blank]"
}

OnClearGuys
{
	LilGuys = IARRAY
	LilGuys ,= "painting"
	Refresh
}