OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	GuyWidth = 240
	GuySpeed = 5
	GuyColor = 0
	GuyBalloon = 0 //on/off
	GuyCap = 10
	LilGuys = IARRAY
	LilGuys ,= "painting"
	SakuraBalloon = 0
	PaintingType = 1
	
	"\1\s[-1]\0%(Refresh)Hello, and welcome to the Simplicity Template, %(username).\w8 As the name implies, this template is very simple.\w8 You will notice there is not much here.\w8 That's on purpose!\w8 This template's primary goal is to make ghost creation less intimidating.\w8 You really need almost nothing to make a ghost;\w4 a basic shell with some interesting randomtalk can be an incredible and fun experience.\w8 So all those boring functions to do with email checking and such?\w8 Not here, unless you decide you want to add them.\w8 Don't let tedious functions tire you out before you can finish your ghost!\w8 Make something simple and fun, or start small and expand upon it later!\w8 Embrace the freedom of simplicity!"
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	if !ISVAR("GuyWidth"); GuyWidth = 240
	if !ISVAR("GuySpeed"); GuySpeed = 5
	if !ISVAR("GuyColor"); GuyColor = 0
	if !ISVAR("GuyBalloon"); GuyBalloon = 0 //on/off
	if !ISVAR("SakuraBalloon"); SakuraBalloon = 0
	if !ISVAR("PaintingType"); PaintingType = 1
	if !ISVAR("GuyCap"); GuyCap = 10
	
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
		GalleryOpen = 0
		LilGuys = IARRAY
		LilGuys ,= "painting"
	}
}

OnBoot
{
	"\0\s[-1]\p[1]\![set,alpha,0]\s[1000]\_w[100]\![get,property,OnFinishBoot,currentghost.scope(1).rect]"
}

//Boot dialogue goes in here!!!!!
OnFinishBoot
{
	_char_left = TOINT(reference0[0])
	_char_right = TOINT(reference0[2])
	GuyWidth = TOINT(_char_right - _char_left)
	if GuyWidth == ""; GuyWidth = 240 //default
	
	"\p[1]\s[-1]\![set,alpha,100]\0"
	--
	Refresh
	--
	if DoFirstbootAgain == 1; "\![raise,OnFirstBoot]"
	else
	{
		"\0\b[0]Normal boot yay - GuyWidth %(GuyWidth)"
	}
}

OnClose
{
	"\0%(Refresh)Farewell."
	"\0%(Refresh)Goodbye."
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}

OnShellScaling
{
	"\p[1]\![set,alpha,0]\s[1000]\![get,property,OnGetWidth,currentghost.scope(1).rect]\s[-1]\![set,alpha,100]"
}